@top Workflow { ImportDecl* TypeDecl* FragmentDecl* WorkflowDecl* }

@external tokens shellTokenizer from "./shell-tokenizer.js" { shellContent }

ImportDecl {
  kw<"import"> "{" ImportList "}" kw<"from"> ImportPath
}

ImportList {
  ImportItem ("," ImportItem)* ","?
}

ImportItem {
  Identifier (kw<"as"> Identifier)?
}

ImportPath {
  String
}

TypeDecl {
  kw<"type"> TypeDeclName "{" TypeField* "}"
}

TypeDeclName {
  identifier
}

TypeField {
  Identifier ":" SchemaType
}

FragmentDecl {
  JobFragmentDecl | StepsFragmentDecl
}

JobFragmentDecl {
  kw<"job_fragment"> Identifier "{" ParamsBlock? JobBody "}"
}

StepsFragmentDecl {
  kw<"steps_fragment"> Identifier "{" ParamsBlock? BlockStep* "}"
}

ParamsBlock {
  kw<"params"> "{" ParamDecl* "}"
}

ParamDecl {
  Identifier ":" SchemaType ("=" Expression)?
}

@skip { whitespace | LineComment | BlockComment }

@detectDelim

WorkflowDecl {
  kw<"workflow"> Identifier "{" WorkflowBody "}"
}

WorkflowBody {
  OnClause?
  (JobDecl | AgentJobDecl | CycleDecl)*
}

OnClause {
  kw<"on"> ":" TriggerSpec
}

TriggerSpec {
  EventName |
  "[" EventList "]"
}

EventList {
  EventName ("," EventName)*
}

EventName {
  identifier
}

JobDecl {
  kw<"job"> Identifier MatrixModifier? ("{" JobBody "}" | "=" FragmentInstantiation)
}

FragmentInstantiation {
  Identifier "{" ParamAssignment* "}"
}

ParamAssignment {
  Identifier ":" Expression
}

MatrixModifier {
  kw<"matrix">
}

AgentJobDecl {
  kw<"agent_job"> Identifier AfterClause? "{" JobBody "}"
}

CycleDecl {
  kw<"cycle"> Identifier "{" CycleBody "}"
}

CycleBody {
  CycleProperty* BodyBlock?
}

CycleProperty {
  MaxItersProperty | KeyProperty | UntilProperty
}

MaxItersProperty {
  kw<"max_iters"> "=" Number
}

KeyProperty {
  kw<"key"> "=" String
}

UntilProperty {
  kw<"until"> GuardJs
}

GuardJs {
  kw<"guard_js"> TripleQuotedString
}

TripleQuotedString {
  tripleString
}

BodyBlock {
  kw<"body"> "{" (JobDecl | AgentJobDecl)* "}"
}

AfterClause {
  kw<"after"> Identifier
}

JobBody {
  JobProperty*
}

JobProperty {
  RunsOnProperty |
  NeedsProperty |
  IfProperty |
  OutputsProperty |
  StepsProperty |
  StepsBlock |
  AxesProperty |
  MaxParallelProperty |
  FailFastProperty |
  IncludeProperty |
  ExcludeProperty
}

IncludeProperty {
  kw<"include"> "[" MatrixCombinationList? "]"
}

ExcludeProperty {
  kw<"exclude"> "[" MatrixCombinationList? "]"
}

MatrixCombinationList {
  MatrixCombination ("," MatrixCombination)*
}

MatrixCombination {
  "{" MatrixCombinationEntryList? "}"
}

MatrixCombinationEntryList {
  MatrixCombinationEntry ("," MatrixCombinationEntry)*
}

MatrixCombinationEntry {
  Identifier ":" MatrixCombinationValue
}

MatrixCombinationValue {
  String | Number | Boolean | HyphenatedIdentifier
}

AxesProperty {
  kw<"axes"> "{" AxisDecl* "}"
}

AxisDecl {
  Identifier ":" "[" AxisValueList "]"
}

AxisValueList {
  AxisValue ("," AxisValue)*
}

AxisValue {
  Number | String | HyphenatedIdentifier
}

HyphenatedIdentifier {
  identifier ("-" identifier)*
}

MaxParallelProperty {
  kw<"max_parallel"> "=" Number
}

FailFastProperty {
  kw<"fail_fast"> "=" Boolean
}

RunsOnProperty {
  kw<"runs_on"> ":" RunnerSpec
}

RunnerSpec {
  identifier ("-" identifier)*
}

NeedsProperty {
  kw<"needs"> ":" NeedsSpec
}

NeedsSpec {
  Identifier |
  "[" IdentifierList "]"
}

IdentifierList {
  Identifier ("," Identifier)*
}

IfProperty {
  kw<"if"> ":" Expression
}

OutputsProperty {
  kw<"outputs"> ":" OutputsBlock
}

OutputsBlock {
  "{" OutputDecl* "}"
}

OutputDecl {
  Identifier ":" OutputType
}

OutputType {
  TypeName | TypeReference
}

TypeName {
  @specialize[@name=TypeName]<identifier, "string" | "int" | "float" | "bool" | "json" | "path">
}

TypeReference {
  identifier
}

StepsProperty {
  kw<"steps"> ":" "[" StepList? "]"
}

StepsBlock {
  kw<"steps"> "{" BlockStep* "}"
}

StepList {
  Step ("," Step)*
}

Step {
  RunStep |
  UsesStep |
  AgentTaskStep |
  GuardJsStep
}

BlockStep {
  ShellStep |
  UsesBlockStep |
  AgentTaskStep |
  GuardJsStep |
  StepsFragmentSpread |
  CheckoutStep
}

CheckoutStep {
  kw<"checkout"> "{" CheckoutProperty* "}"
}

CheckoutProperty {
  WithProperty
}

StepsFragmentSpread {
  "..." Identifier "{" ParamAssignment* "}"
}

ShellStep {
  kw<"shell"> ShellBlock
}

ShellBlock {
  shellContent
}

UsesBlockStep {
  kw<"uses"> "(" String ")" UsesConfigBlock
}

UsesConfigBlock {
  "{" UsesConfigProperty* "}"
}

UsesConfigProperty {
  WithProperty
}

WithProperty {
  kw<"with"> ":" ObjectLiteral
}

ObjectLiteral {
  "{" ObjectPropertyList? "}"
}

ObjectPropertyList {
  ObjectProperty ("," ObjectProperty)* ","?
}

ObjectProperty {
  ObjectPropertyKey ":" ObjectValue
}

ObjectPropertyKey {
  Identifier | TypeName
}

ObjectValue {
  String |
  Number |
  Boolean |
  ObjectLiteral |
  ArrayLiteral
}

ArrayLiteral {
  "[" ArrayItems? "]"
}

ArrayItems {
  ObjectValue ("," ObjectValue)*
}

GuardJsStep {
  kw<"step"> String GuardJs
}

RunStep {
  kw<"run"> "(" String ")"
}

UsesStep {
  kw<"uses"> "(" String ")"
}

AgentTaskStep {
  kw<"agent_task"> "(" String ")" "{" AgentTaskBody "}"
}

AgentTaskBody {
  AgentTaskProperty*
}

AgentTaskProperty {
  ModelProperty |
  MaxTurnsProperty |
  ToolsProperty |
  McpProperty |
  SystemPromptProperty |
  PromptProperty |
  OutputSchemaProperty |
  OutputArtifactProperty |
  ConsumesProperty
}

ModelProperty {
  kw<"model"> ":" String
}

MaxTurnsProperty {
  kw<"max_turns"> ":" Number
}

ToolsProperty {
  kw<"tools"> ":" ToolsBlock
}

ToolsBlock {
  "{" ToolsBlockProperty* "}"
}

ToolsBlockProperty {
  AllowedProperty |
  DisallowedProperty |
  StrictProperty
}

AllowedProperty {
  kw<"allowed"> ":" StringListOrAll
}

DisallowedProperty {
  kw<"disallowed"> ":" StringList
}

StrictProperty {
  kw<"strict"> ":" Boolean
}

StringListOrAll {
  "*" |
  StringList
}

StringList {
  "[" StringListItems? "]"
}

StringListItems {
  String ("," String)*
}

McpProperty {
  kw<"mcp"> ":" McpBlock
}

McpBlock {
  "{" McpBlockProperty* "}"
}

McpBlockProperty {
  ConfigFileProperty |
  AllowedProperty |
  DisallowedProperty
}

ConfigFileProperty {
  kw<"config_file"> ":" String
}

SystemPromptProperty {
  kw<"system_prompt"> ":" PromptValue
}

PromptProperty {
  kw<"prompt"> ":" PromptValue
}

PromptValue {
  String |
  FileReference |
  TemplateReference
}

FileReference {
  kw<"file"> "(" String ")"
}

TemplateReference {
  kw<"template"> "(" String ")"
}

OutputSchemaProperty {
  kw<"output_schema"> ":" (String | InlineSchema | SchemaTypeReference)
}

SchemaTypeReference {
  identifier
}

InlineSchema { "{" SchemaField* "}" }

SchemaField { Identifier ":" SchemaType }

SchemaType {
  UnionType |
  NonUnionSchemaType
}

NonUnionSchemaType {
  ArrayType |
  ObjectType |
  SchemaPrimitiveType |
  NullType |
  StringLiteralType
}

SchemaPrimitiveType { identifier | TypeName }

ArrayType { "[" SchemaType "]" }

ObjectType { "{" SchemaField* "}" }

UnionType { NonUnionSchemaType ("|" NonUnionSchemaType)+ }

NullType {
  @specialize[@name=NullType]<identifier, "null">
}

StringLiteralType { String }

OutputArtifactProperty {
  kw<"output_artifact"> ":" String
}

ConsumesProperty {
  kw<"consumes"> ":" ConsumesBlock
}

ConsumesBlock {
  "{" ConsumesItem* "}"
}

ConsumesItem {
  Identifier ":" kw<"from"> "(" String ")"
}

Expression {
  ComparisonExpr |
  PrimaryExpr
}

ComparisonExpr {
  PrimaryExpr ComparisonOp PrimaryExpr
}

PrimaryExpr {
  PropertyAccess |
  String |
  Boolean |
  Number |
  "(" Expression ")"
}

PropertyAccess {
  identifier ("." identifier)*
}

ComparisonOp {
  "==" | "!="
}

Boolean {
  @specialize[@name=Boolean]<identifier, "true" | "false">
}

Identifier {
  identifier
}

String {
  string
}

Number {
  number
}

@tokens {
  whitespace { @whitespace+ }

  LineComment { "//" ![\n]* }

  BlockComment { "/*" blockCommentContent* "*/" }

  blockCommentContent { ![\*] | "*" ![\\/] }

  identifier { $[a-zA-Z_] $[a-zA-Z0-9_]* }

  string { '"' (![\\\"\n] | "\\" _)* '"' }

  tripleString { '"""' (!["] | '"' !["] | '""' !["])* '"""' }

  number { $[0-9]+ }

  "{"
  "}"
  "["
  "]"
  "("
  ")"
  ":"
  ","
  "."
  "..."
  "-"
  "*"
  "="
  "=="
  "!="
  "|"

  @precedence { "...", "==", "!=", "=", "-", ".", tripleString, number, identifier }
}

kw<term> { @specialize[@name={term}]<identifier, term> }
